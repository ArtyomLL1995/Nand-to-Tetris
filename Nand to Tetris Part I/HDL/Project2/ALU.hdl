// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x,b=false,sel=zx,out=zxOut);
    Not16(in=zxOut,out=notZxOut);
    Mux16(a=zxOut,b=notZxOut,sel=nx,out=nxOut);

    Mux16(a=y,b=false,sel=zy,out=zyOut);
    Not16(in=zyOut,out=notZyOut);
    Mux16(a=zyOut,b=notZyOut,sel=ny,out=nyOut);

    Add16(a=nxOut,b=nyOut,out=addNxNyOut);
    And16(a=nxOut,b=nyOut,out=andNxNyOut);
    Mux16(a=andNxNyOut,b=addNxNyOut,sel=f,out=fOut);

    Not16(in=fOut,out=notF);
    Mux16(a=fOut,b=notF,sel=no,out=preOut);

    Mux16(a=fOut, b=notF, sel=no, out[15]=s, out[0..14]=t);
    Mux16(a=fOut, b=notF, sel=no, out[0..7]=mux16One, out[8..15]=mux16Two);
    Or8Way(in=mux16One, out=Or8WayOut1);
    Or8Way(in=mux16Two, out=Or8WayOut2);
    Or(a=Or8WayOut1, b=Or8WayOut2, out=OrOut1);
    Not(in=OrOut1, out=zr);
    Mux16(a=fOut, b=notF, sel=no, out=out);
    Mux(a=false, b=true, sel=s, out=ng);
}