class Keyboard {

    static Array memory;

    static char currentlyPressedKey;

    function void init() {
        let memory = 24576;
        return;
    } 

    function char keyPressed() {
        return memory[0];
    }

    function char readChar() {
        var char c;
        do Output.printChar(0);
        while(Keyboard.keyPressed() = 0) {}
        let currentlyPressedKey = Keyboard.keyPressed();
        while (~(Keyboard.keyPressed()=0)) {}
        let c = currentlyPressedKey;
        do Output.backSpace();
        do Output.printChar(c);
        return c;
    }

    function String readLine(String message) {
        var char c;
        var String str;
        if (message.length() > 0) {
            do Output.printString(message);
        }
        let str = String.new(200);
        while (true) {
            let c = Keyboard.readChar();
            if ((c = 128) | (c = 129)) {
                if (c = 128) {
                    do Output.println();
                    return str;
                } else {
                    do str.eraseLastChar();
                    do Output.backSpace();
                }
            } else {
                let str = str.appendChar(c);
            }
        }
        return str;
    } 

    function int readInt(String message) {
        var String line;
        let line = Keyboard.readLine(message);
        return line.intValue();
    }

}